buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("se.transmode.gradle:gradle-docker:${dockerPluginVersion}")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
    }
}

apply plugin: 'docker'
apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        languageVersion = "1.2"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        languageVersion = "1.2"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile("org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}",
            "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")
    compile("org.springframework.boot:spring-boot-starter-actuator",
            "org.springframework.boot:spring-boot-starter-aop",
            "org.springframework.boot:spring-boot-starter-amqp",
            "org.springframework.boot:spring-boot-starter-security",
            "org.springframework.boot:spring-boot-starter-web")
    compile("io.github.microutils:kotlin-logging:${kotlinLoggingVersion}")
    compile("com.fasterxml.jackson.module:jackson-module-kotlin")
    compile("io.github.openfeign:feign-core:${feignVersion}",
            "io.github.openfeign:feign-jackson:${feignVersion}",
            "io.github.openfeign:feign-slf4j:${feignVersion}")
    compile("com.fasterxml.jackson.module:jackson-module-kotlin")

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}",
            "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}")
    testCompile("com.nhaarman.mockitokotlin2:mockito-kotlin:${mockitoKotlinVersion}")
    testCompile("info.novatec.testit:testutils-logrecorder-logback:${logRecorderVersion}")
    testCompile("org.springframework.cloud:spring-cloud-contract-wiremock:${springCloudWiremockVersion}")
    testCompile("au.com.dius:pact-jvm-consumer_2.12:${pactVersion}",
            "au.com.dius:pact-jvm-consumer-junit_2.12:${pactVersion}")


    testRuntime("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}",
            "org.junit.platform:junit-platform-launcher:${junitPlatformVersion}")
}

springBoot {
    buildInfo()
}

task unitTest(type: Test) {
    useJUnitPlatform() { includeTags 'unit-test' }
    testLogging { events "skipped", "failed" }
}

task integrationTest(type: Test) {
    dependsOn unitTest
    useJUnitPlatform() { includeTags 'integration-test' }
    testLogging { events "skipped", "failed" }
}

task contractTest(type: Test) {
    dependsOn integrationTest
    useJUnitPlatform() { includeTags 'contract-test' }
    testLogging { events "skipped", "failed" }
}

task aggregateTestReports(type: TestReport) {
    destinationDir = test.reports.html.destination
    reportOn unitTest, integrationTest, contractTest
}

tasks.check.dependsOn.remove(test)
tasks.check.dependsOn contractTest